ident       : /[a-zA-Z_][a-zA-Z0-9_]*/ | /[a-zA-Z_]/;
scoped      : <ident> ("::" <ident>)* ;
generic     : <scoped> '<' <generic>? (',' <generic>)* '>'
            | <scoped> ;
attribs     : "[[" <scoped> (',' <scoped>)* "]]" ;
field       : <attribs>? <generic> <ident> ;
fields      : (<field> ';')+ ;
param       : "const"? <generic> ("&&"|"&")? <ident> ;
params      : <param>? (',' <param>)* ;
method      : <attribs>? <generic> <ident> '(' <params>? ')' ;
methods     : (<method> ';')+ ;
meta        : "meta" <attribs>? <ident> '{' <fields> '}' ;
rpc         : ("service"|"srpc"|"jrpc")  <attribs>? <ident> '{' <methods> '}' ;
types       : ((<meta>|<rpc>) ';')+ ;
namespace   : "namespace" <scoped> '{' <types> '}' ;
symbol      : "symbol" '(' <ident> ')' ;
symbols     : <symbol>+ ;
include     : "#include" (('"' /(\\.|[^"])+/ '"') | ('<' /(\\.|[^>])+/ '>' )) ;
includes    : <include>+ ;
program     : <includes>? <symbols>? <namespace> ;